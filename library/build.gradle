apply plugin: 'com.android.library'
//apply from: "upload.gradle"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
//上传到jcenter配置
//apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a"// 指定ndk需要兼容的ABI(这样其他依赖包里x86,armeabi,arm-v8之类的so会被过滤掉)
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0-beta01'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//publish {
//    userOrg = 'lastdeity'//bintray.com用户名
//    repoName = 'Android-lib'//默认的仓库名称
//    groupId = 'com.deity.texttospeech'//jcenter上的路径
//    artifactId = 'LibBaiduTextToSpeech'//项目名称
//    publishVersion = '1.1.0'//版本号
//    desc = 'Baidu Text To Speech Library'
//    website = 'https://github.com/MeDeity/LibBaiduTextToSpeech'
//}


// 声明当前的版本号
version = "1.1.0"
//定义当前library的站点
def siteUrl = 'https://github.com/MeDeity/LibBaiduTextToSpeech'    // project homepage
def gitUrl = 'https://github.com/MeDeity/LibBaiduTextToSpeech' // project git

//定义组织,这样引用该library就变成:compile 'com.deity.common:common:1.0.0‘
group = "com.deity.texttospeech"

//上传到jcenter至少需要四个文件，除了打包的aar之外，还需要pom和javadoc，source，否则是通不过jcenter的审核。这些我们都可以用脚本生成。

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成文档
task javadoc(type: Javadoc) {
    //编码GBK的不可映射字符->请正确配置javadoc编码
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 根节点添加
install {
    repositories.mavenInstaller {// This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Text To Speech For Android(Baidu)'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id 'lastDeity'
                        name 'wenhua.feng'
                        email 'langrenbule@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


//配置bintray参数
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "Android-lib"           //跟jcenter上创建的Maven仓库名字保持一致
        name = "LibBaiduTextToSpeech"      //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
//如何上传
//在local.properties中加入
//bintrayUser=xxx
//bintrayApiKey=xxxx
//在Terminal中执行gradlew bintrayUpload